# QueueLib | [MAIN] | [NAMESPACES] | [METRICS] | [BACK]  

## Documentation for Program FIFO_FWFT_UnitTest  

```pascal
//Automated unit tests for function block FIFO_FWFT_Int  
INTERFACE
    VAR 
        vRunTests : BOOL := FALSE; (*Tap to TRUE, to run test*)
        vReturnsTrueOnSuccEnqOk : BOOL := FALSE; (*Results*)
        vFillsThreeValuesOk : BOOL := FALSE;
        vReportsFullOk : BOOL := FALSE;
        vReturnsFalseOnUnSuccEnqOk : BOOL := FALSE;
        vReturnsTrueOnSuccDeqOk : BOOL := FALSE;
        vDequeue1Ok : BOOL := FALSE;
        vDequeue2Ok : BOOL := FALSE;
        vCountingWorks : BOOL := FALSE;
        vReportsEmptyOk : BOOL := FALSE;
        vReturnsTrueOnUnSuccDeqOk : BOOL := FALSE;
        vFifoFWFTOk : BOOL := FALSE;
        vPeekOk : BOOL := FALSE;
        fifo : FIFO_FWFT_Int;
        vFifoBuffer : ARRAY[0..5] OF INT;
        vTempInt : INT;
        vTempIdx : UINT;
    END_VAR
END_INTERFACE
PROGRAM FIFO_FWFT_UnitTest:
    (*Init FIFO*)
    fifo.FB_Init(bInitRetains := TRUE, bInCopyCode := TRUE); //Implicit function block init, curtesy of codesys. To init function block on every run
    SysMemSet(ADR(vFifoBuffer), 0, SIZEOF(vFifoBuffer));
    fifo.Buffer := ADR(vFifoBuffer);
    fifo.Size   := SIZEOF(vFifoBuffer)/SIZEOF(INT);
    (***List of tests***)
    (***Test1: Fifo returns TRUE on successfull enqueue***)
    vReturnsTrueOnSuccEnqOk := fifo.Enqueue(1);

    (***Test2: Fifo fills three values correctly***)
    fifo.Enqueue(2);
    fifo.Enqueue(3);

    vFillsThreeValuesOk := vFifoBuffer[0] = 1 AND vFifoBuffer[1] = 2 AND vFifoBuffer[2] = 3;

    (***Test3: Fifo reports FULL when full***)
    fifo.Enqueue(4);
    fifo.Enqueue(5);
    fifo.Enqueue(6);

    vReportsFullOk := fifo.isFull();

    (***Test4: Fifo reports FALSE on unsuccesfull enqueue***)
    vReturnsFalseOnUnSuccEnqOk := NOT fifo.Enqueue(7);

    (***Test5: Fifo returns TRUE on successfull dequeue***)
    vReturnsTrueOnSuccDeqOk := fifo.Dequeue(Value => vTempInt);

    (***Test6: Fifo dequeues values correctly***)
    fifo.Dequeue(Value => vTempInt);
    vDequeue1Ok := vTempInt = 2;
    fifo.Dequeue(Value => vTempInt);
    vDequeue2Ok := vTempInt = 3;

    (***Test7: Fifo can count correctly***)
    vCountingWorks := fifo.NrElements = 3; //At this point in the tests, there should be 3 elements

    (***Test8: Fifo reports EMPTY when EMPTY***)
    fifo.Dequeue(Value => vTempInt); //Returns 4
    fifo.Dequeue(Value => vTempInt); //Returns 5
    fifo.Dequeue(Value => vTempInt); //Returns 6
    vReportsEmptyOk := fifo.isEmpty();

    (***Test9: Fifo reports FALSE on unsuccesfull dequeue***)
    vReturnsTrueOnUnSuccDeqOk := NOT fifo.Dequeue();

    (***Test10: Fifo first word fall trough is working***)
    fifo.Enqueue(1);
    fifo.Enqueue(2);
    fifo.Enqueue(3);
    fifo.Dequeue(Value => vTempInt); //Returns 1
    fifo.Enqueue(4);
    fifo.Enqueue(5);
    fifo.Enqueue(6); //Here it is not full, as the first array index is free, compared to Standard fifo, we shifted the whole array to the left, index 0 should hold 2
    vFifoFWFTOk := NOT fifo.isFull() AND fifo.Enqueue(7) AND vFifoBuffer[0] = 2;

    (***Test11: Peek functionality: Should return the next element but not remove it, it should report correct index of the buffer***)
    vTempIdx := fifo.Peek(Value => vTempInt);
    vPeekOk := fifo.NrElements = 6 AND vTempInt = 2 AND vTempIdx = 0;

    vRunTests := FALSE;
END_PROGRAM
```

## Metrics  

- VAR : 17

| Actions | Methods | Lines of code | Lines of comments | Lines in total | Maintainable size |
| ------- | ------- | ------------- | ----------------- | -------------- | ----------------- |
| 0 | 0 | 35 |13 |61 | 52 |

---
Autogenerated with [ia_tools](https://github.com/tkucic/ia_tools)  

[MAIN]: ../../../../index_st.md
[NAMESPACES]: ../../nsList_st.md
[METRICS]: ../../../metrics_st.md
[BACK]: ../nsMain_st.md
