# QueueLib | [MAIN] | [NAMESPACES] | [METRICS] | [BACK]  

## Documentation for Program FIFO_OP_UnitTest  

```pascal
//Automated unit tests for function block FIFO_OP_Int  
INTERFACE
    VAR 
        vRunTests : BOOL := FALSE; (*Tap to TRUE, to run test*)
        vReturnsTrueOnSuccEnqOk : BOOL := FALSE; (*Results*)
        vFillsThreeValuesOk : BOOL := FALSE;
        vReportsFullOk : BOOL := FALSE;
        vReturnsFalseOnUnSuccEnqOk : BOOL := FALSE;
        vReturnsTrueOnSuccDeqOk : BOOL := FALSE;
        vDequeue1Ok : BOOL := FALSE;
        vDequeue2Ok : BOOL := FALSE;
        vCountingWorks : BOOL := FALSE;
        vReportsEmptyOk : BOOL := FALSE;
        vReturnsTrueOnUnSuccDeqOk : BOOL := FALSE;
        vFifoReturnsHighestPrioValue : BOOL := FALSE;
        vFifoBufferIsOrdered : BOOL := FALSE;
        vPeekOk : BOOL := FALSE;
        fifo : FIFO_OP_Int;
        vFifoBuffer : ARRAY[0..5] OF dtFIFO_P_Element_Int;
        vTempInt : INT;
        vTempPrio : INT;
        vTempIdx : UINT;
    END_VAR
END_INTERFACE
PROGRAM FIFO_OP_UnitTest:
    (*Init FIFO*)
    fifo.FB_Init(bInitRetains := TRUE, bInCopyCode := TRUE); //Implicit function block init, curtesy of codesys. To init function block on every run
    SysMemSet(ADR(vFifoBuffer), 0, SIZEOF(vFifoBuffer));
    fifo.Buffer := ADR(vFifoBuffer);
    fifo.Size   := SIZEOF(vFifoBuffer)/SIZEOF(dtFIFO_P_Element_Int);
    (***List of tests***)
    (***Test1: Fifo returns TRUE on successfull enqueue***)
    vReturnsTrueOnSuccEnqOk := fifo.Enqueue(1,1);

    (***Test2: Fifo fills three values correctly***)
    fifo.Enqueue(2,2);
    fifo.Enqueue(3,3);

    vFillsThreeValuesOk := vFifoBuffer[0].Value = 1 AND vFifoBuffer[0].Priority = 1 AND
                           vFifoBuffer[1].Value = 2 AND vFifoBuffer[1].Priority = 2 AND
                           vFifoBuffer[2].Value = 3 AND vFifoBuffer[2].Priority = 3;

    (***Test3: Fifo reports FULL when full***)
    fifo.Enqueue(4,4);
    fifo.Enqueue(5,5);
    fifo.Enqueue(6,6);

    vReportsFullOk := fifo.isFull();

    (***Test4: Fifo reports FALSE on unsuccesfull enqueue***)
    vReturnsFalseOnUnSuccEnqOk := NOT fifo.Enqueue(7,7);

    (***Test5: Fifo returns TRUE on successfull dequeue***)
    vReturnsTrueOnSuccDeqOk := fifo.Dequeue(Value => vTempInt); //Returns 6,6

    (***Test6: Fifo dequeues values correctly***)
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio);
    vDequeue1Ok := vTempInt = 5 AND vTempPrio = 5; //Should return the highest priority value, in this case it should be 5,5
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio);
    vDequeue2Ok := vTempInt = 4 AND vTempPrio = 4; //Should return the highest priority value, in this case it should be 4,4

    (***Test7: Fifo can count correctly***)
    vCountingWorks := fifo.NrElements = 3; //At this point in the tests, there should be 3 elements

    (***Test8: Fifo reports EMPTY when EMPTY***)
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio); //Returns 3,3
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio); //Returns 2,2
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio); //Returns 1,1
    vReportsEmptyOk := fifo.isEmpty();

    (***Test9: Fifo reports FALSE on unsuccesfull dequeue***)
    vReturnsTrueOnUnSuccDeqOk := NOT fifo.Dequeue();

    (***Test10: Fifo returns the highest priority value first***)
    fifo.Enqueue(1,100);
    fifo.Enqueue(2,50);
    fifo.Enqueue(3,25);
    fifo.Enqueue(4,200);
    fifo.Enqueue(5,11);
    fifo.Enqueue(6,-6);
    fifo.Dequeue(Value => vTempInt, Priority => vTempPrio);
    vFifoReturnsHighestPrioValue := vTempInt = 4 AND vTempPrio = 200;

    (***Test11: Fifo has ordered the buffer correctly according to priority. The buffer is lowest : highest = 0 : N***)
    vFifoBufferIsOrdered := vFifoBuffer[0].Value = 6 AND vFifoBuffer[1].Value = 5 AND vFifoBuffer[2].Value = 3 AND vFifoBuffer[3].Value = 2 AND vFifoBuffer[4].Value = 1;

    (***Test12: Peek functionality: Should return the next element but not remove it, it should report correct index of the buffer***)
    vTempIdx := fifo.Peek(Value => vTempInt, Priority => vTempPrio);
    vPeekOk := fifo.NrElements = 5 AND vTempInt = 1 AND vTempIdx = 4;

    vRunTests := FALSE;
END_PROGRAM
```

## Metrics  

- VAR : 19

| Actions | Methods | Lines of code | Lines of comments | Lines in total | Maintainable size |
| ------- | ------- | ------------- | ----------------- | -------------- | ----------------- |
| 0 | 0 | 38 |14 |66 | 57 |

---
Autogenerated with [ia_tools](https://github.com/tkucic/ia_tools)  

[MAIN]: ../../../../index_st.md
[NAMESPACES]: ../../nsList_st.md
[METRICS]: ../../../metrics_st.md
[BACK]: ../nsMain_st.md
